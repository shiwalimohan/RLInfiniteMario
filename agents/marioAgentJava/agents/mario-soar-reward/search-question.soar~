sp {propose*search-question
   (state <s> ^name mario-soar
              ^question <q>)
   (<q> ^isreachable yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name search-question
        ^question <q>)
}
sp {search-question-general
   :template
   (state <s> ^name mario-soar
              ^operator <op> +)
   (<op> ^name search-question
         ^question <q>)
   (<q> ^distx <dx>
        ^disty <dy>)
-->
   (<s> ^operator <op> = 0)
}


sp {elaborate*psuedo*reward*question
   (state <s> ^name search-question
              ^superstate <ss>
              ^question <q>
              ^reward-link <r>
              ^io.input-link.visual-scene.tile-row <row>)
   (<q> ^x <x>
        ^y <y>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <x>
        -^type |?|)
-->
   (<r> ^reward.value 1)
}

sp {propose*form*state*search-question
   (state <s> ^name search-question
              ^superstate <ss>
             -^copied yes)
   (<ss> ^operator <sop>)
   (<sop> ^name search-question
          ^question <q>)
-->
   (<s> ^operator <op>)
   (<op> ^name copy-question
        ^question <q>)
}

sp {apply*form*state*search-question
   (state <s> ^name search-question
              ^operator <op>)
   (<op> ^name copy-question
         ^question <q>)
-->
   (<s> ^question <q>
        ^copied yes)
}
sp {question*random*action
   (state <s> ^name search-question
              ^rand-move <mv>
              ^rand-jump <jp>
              ^rand-speed <sp>
              ^mario <mario>
              ^copied yes)
   (<mv> ^direction <d>)
   (<jp> ^up <j>)
   (<mario> ^xi <mx>
            ^yi <my>)
   (<sp> ^degree <sd>)
-->
   (<s> ^operator <o> +)
   (<o> ^name action
        ^direction <d>
        ^jump <j>
        ^speed <sd>)
}
sp {search-question
   :template
   (state <s> ^name search-question
              ^operator <op> +
              ^question <q>
              ^mario <m>)
   (<m> ^type <mt>
        ^tile-above <ta>)
   (<q> ^distx <dx>
        ^disty <dy>
        ^isreachable yes)
   (<op> ^name action
         ^direction <d>
         ^jump <j>
         ^speed <sd>)
--> 
   (<s> ^operator <op> = 0)
}