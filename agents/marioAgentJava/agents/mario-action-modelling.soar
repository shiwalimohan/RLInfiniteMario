#-------------general soar rules to handle state and related elaborations--------------------------------

sp {all*elaborate-state-name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

sp {all*elaborate-state-top-state
   (state <s> ^superstate.top-state <ts>)
-->
   (<s> ^top-state <ts>)
}

sp {all*copy*reward
   (state <s> ^reward-link <r>
              ^io.input-link <il>)
   (<il> ^reward <num>)
-->
   (<r> ^reward.value <num>)
}

sp {elaborate*state*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

sp {elaborate*state*top-state
   (state <s> ^superstate.top-state <ts>)
-->
   (<s> ^top-state <ts>)
}
sp {elaborate*state*io
   (state <s> ^superstate.io <io>)
-->
   (<s> ^io <io>)
}
 
#--------------------mario specific elaborations----------------------------------------------------------

#----------------elaborate mario--------------------
sp {mario-soar*elaborate-mario
   (state <s> ^io.input-link.mario <mario>
              ^name mario-soar)
-->
   (<s> ^mario <mario>)
}

#----------------elaborate monsters-----------------

sp {mario-soar*elaborate-monster
   (state <s> ^io.input-link.monsters.monster <m>
              ^name mario-soar)
   -(<m> ^type |Mario|)
-->
   (<s> ^monster <m>)
}
sp {mario-soar*elaborate-monster-relative-distance
   (state <s> ^mario <mario>
              ^monster <ob>)
   (<mario> ^x <mariox>
            ^y <marioy>)
   (<ob> ^x <obx>
         ^y <oby>)
-->
   (<ob> ^distx (- <obx> <mariox>)
         ^disty (- <oby> <marioy>))
}

#------------------elaborate tiles--------------------------
sp {mario-soar*elaborate-xright
   (state <s> ^mario <mario>
   	      ^name mario-soar)
   (<mario> ^xi <x>
        ^yi <y>)
-->
   (<s> ^xtile <xt>
        ^ytile <yt>)
   (<xt> ^right (+ <x> 1)
         ^left (- <x> 1)
         ^centre <x>)
}

sp {mario-soar*elaborate-tile-on-right
   (state <s> ^io.input-link <il>
              ^xtile.right <xloc>
              ^mario.yi <y>
	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <xloc>
        ^type <ty>)
-->
   (<s> ^tile-type.right <ty>)
}
sp {mario-soar*elaborate-coin
   (state <s> ^io.input-link <il>
   	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <x>
        ^type |$|)
-->
   (<s> ^coin <c>)
   (<c> ^x <x>
        ^y <y>)
}
sp {mario-soar*elaborate-coin-reachable
   (state <s> ^coin <c>
   	      ^name mario-soar)
   (<c> ^distx < 2
        ^disty < 4)
  -(<c> ^distx < -1)
  -(<c> ^disty < 0)
-->
   (<c> ^isreachable yes)
}
sp {mario-soar*elaborate-question
   (state <s> ^io.input-link <il>
   	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <x>
        ^type |?|)
-->
   (<s> ^question <q>)
   (<q> ^x <x>
        ^y <y>)
}
sp {mario-soar*elaborate-question-reachable
   (state <s> ^question <q>
   	      ^name mario-soar)
   (<q> ^distx < 3
        ^disty < 4)
  -(<q> ^distx < -2)
  -(<q> ^disty < 0)
-->
   (<q> ^isreachable yes)
}
sp {mario-soar*elaborate-coin-relative-distance
   (state <s> ^mario <mario>
              ^coin <ob>
   	      ^name mario-soar)
   (<mario> ^xi <mariox>
            ^yi <marioy>)
   (<ob> ^x <obx>
         ^y <oby>)
-->
   (<ob> ^distx (- <obx> <mariox>)
         ^disty (- <oby> <marioy>))
}
sp {mario-soar*elaborate-question-relative-distance
   (state <s> ^mario <mario>
              ^question <ob>
   	      ^name mario-soar)
   (<mario> ^xi <mariox>
            ^yi <marioy>)
   (<ob> ^x <obx>
         ^y <oby>)
-->
   (<ob> ^distx (- <obx> <mariox>)
         ^disty (- <oby> <marioy>))
}

#------------------intialize-agent-----------------------

sp {nil*propose*initialize-mario-soar
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name initialize-mario-soar)
}

sp {nil*apply*initialize-mario-soar
   (state <s> ^operator <op>)
   (<op> ^name initialize-mario-soar)
-->
   (<s> ^name mario-soar
        ^rand-move <m1>
        ^rand-move <m2>
        ^rand-move <m3>
        ^rand-jump <j1>
        ^rand-jump <j2>
        ^rand-speed <s1>
        ^rand-speed <s2>)
   (<m1> ^direction left)
   (<m2> ^direction right)
   (<m3> ^direction nil)
   (<j1> ^up yes)
   (<j2> ^up no)
   (<s1> ^degree low)
   (<s2> ^degree high)
}

#-------------------