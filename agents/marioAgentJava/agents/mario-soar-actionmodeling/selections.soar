#---------------------------------------------------------------------
#  no need to order evaluation-operator
#  propose, elaborate evaluation-operator
#---------------------------------------------------------------------

sp {selection*propose*evaluate-operator
   :default
   (state <s> ^name selection
   	      ^item <i>)
 -{(state <s> ^evaluation <e>)
   (<e> ^superoperator <i>
   	^value true)}
-->
  (<s> ^operator <o> +)
  (<o> ^name evaluate-operator
       ^superoperator <i>)
}

sp {selection*select*ewaluate-operator*indifferent
   :default
   (state <s> ^name selection
	      ^operator <o> +)
   (<o> ^name evaluate-operator)
-->
   (<s> ^operator <o> =)
}

sp {selection*elaborate*evaluate-operator*evaluation
   :default
   (state <s> ^name selection
   	      ^operator <o>
	      ^evaluation <e>)
   (<o> ^name evaluate-operator
   	^superoperator <ob>)
   (<e> ^superoperator <ob>)
-->
   (<o> ^evaluation <e>)
}

sp {selection*elaborate*evaluate-operator*superproblem-space
   :default
   (state <s> ^name selection
   	      ^operator <o>
	      ^superstate.problem-space <p>)
   (<o> ^name evaluate-operator)
-->
   (<o> ^superproblem-space <p>)
}

sp {selection*elaborate*evaluate-operator*superstate
   :default
   (state <s> ^name selection
   	      ^operator <o>
	      ^superstate <ss>)
   (<o> ^name evaluate-operator)
-->
   (<o> ^superstate <ss>)
}

sp {selection*elaborate*state*all-objects-evaluated
   :default
   (state <s> ^name selection
   	      ^operator <o> +)
 - {(<o> ^name evaluate-operator 
   	 ^evaluation.value true)}
-->
  (<s> ^not-all-operators-evaluated true)
}

sp {selection*apply*state*evaluation
   :default
   (state <s> ^name selection
   	      ^operator <o>)
   (<o> ^name evaluate-operator
   	^superoperator <ob>)
-->
  (<s> ^evaluation <e>)
  (<e> ^superoperator <ob>)
}

sp {monitor*operator*evaluation
   :default
   (state <s> ^top-state <ts>
   	      ^operator <o>)
   (<o> ^name evaluate-operator
       	^superoperator <obj>
	^evaluation. << numeric-value symbolic-value >> <n>)
   (<obj> ^name <name>)
   (<ts> ^superstate nil
   	 -^verbose false)
--> 
   (write (crlf) | Evaluation of | <obj> | (| <name> |) is | <n>)}

