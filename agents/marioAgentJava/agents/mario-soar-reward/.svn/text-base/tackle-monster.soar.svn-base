sp {propose*tackle-monster
   (state <s> ^name mario-soar
              ^monster <m>)
   (<m> ^isthreat yes)
  -(<m> ^isthreat no)
-->
   (<s> ^operator <o> +)
   (<o> ^name tackle-monster
        ^monster <m>)
}

sp {tackle*monster*general
    :template
   (state <s> ^name mario-soar
              ^operator <op> +)
   (<op> ^name tackle-monster
         ^monster <m>)
   (<m> ^distx <distx>
        ^disty <disty>)
-->
   (<s> ^operator <op> = 0)
}

sp {elaborate*psuedo*reward
   (state <s> ^name tackle-monster
              ^monster <m>
             ^reward-link <r>
             -^io.input-link.monsters.monster <m>)
-->
   (<r> ^reward.value 1)
}

sp {elaborate*psuedo*reward*1
   (state <s> ^name tackle-monster
              ^monster <m>
              ^reward-link <r>
              ^superstate.monster <m>)
   (<m> ^distx < -3)
-->
   (<r> ^reward.value 1)
}

sp {propose*form*state*tackle-monster
   (state <s> ^name tackle-monster
              ^superstate <ss>
             -^copied yes)
   (<ss> ^operator <sop>)
   (<sop> ^name tackle-monster
          ^monster <m>)
-->
   (<s> ^operator <op>)
   (<op> ^name copy-monster
        ^monster <m>)
}

sp {apply*form*State*tackle-monster
   (state <s> ^name tackle-monster
              ^operator <op>)
   (<op> ^name copy-monster
         ^monster <m>)
-->
   (<s> ^monster <m>
        ^copied yes)
}


sp {monster*random*action
   (state <s> ^name tackle-monster
              ^rand-move <mv>
              ^rand-jump <jp>
              ^rand-speed <sp>
              ^monster <m>
              ^copied yes
              ^mario <mario>)
   (<m> ^distx <dx>
        ^disty <dy>)
   (<jp> ^up <j>) 
   (<mario> ^yi <my>)
   (<mv> ^direction <d>)
   (<sp> ^degree <sd>)
-->
   (<s> ^operator <o> +)
   (<o> ^name action
        ^direction <d>
        ^jump <j>
        ^speed <sd>)
}
sp {tackle*monster
   :template
   (state <s> ^name tackle-monster
              ^operator <op> +
              ^superstate <ss>
              ^monster <m>
              ^superstate.mario <mario>)
   (<mario> ^type <mtype>
            ^tile-above <ta>)
   (<op> ^name action
         ^direction <dir>
         ^jump <jp>
         ^speed <sp>)
   (<m> ^type <monst>
        ^distx <dx>
        ^disty <dy>)
--> 
   (<s> ^operator <op> = 0)
}
