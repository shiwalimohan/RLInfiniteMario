sp {propose*tackle-monster
   (state <s> ^name mario-soar-common
              ^monster <m>)
   (<m> ^isthreat yes
        -^type |Fireball|)
  -(<m> ^isthreat no)
-->
   (<s> ^operator <o> +)
   (<o> ^name tackle-monster
        ^monster <m>)
}
sp {tackle*monster*general
   :template
   (state <s> ^name mario-soar-common
              ^operator <op> +)
   (<op> ^name tackle-monster
         ^monster <m>)
   (<m> ^type <monst>
        ^distx <dx>
        ^disty <dy>
        ^direction <dir>)
-->
   (<s> ^operator <op> = 0)
}
sp {monster*random*action
   (state <s> ^name tackle-monster
              ^rand-move <mv>
              ^rand-jump <jp>
              ^rand-speed <sp>
              ^monster <m>
              ^mario <mario>)
   (<m> ^distx <dx>
        ^disty <dy>
        ^direction <dir>)
   (<jp> ^up <j>) 
   (<mario> ^yi <my>)
   (<mv> ^direction <d>)
   (<sp> ^degree <sd>)
-->
   (<s> ^operator <o> +)
   (<o> ^name action
        ^direction <d>
        ^jump <j>
        ^speed <sd>)
}
sp {tackle*monster
   :template
   (state <s> ^name tackle-monster
              ^operator <op> +
              ^monster <m>
              ^mario <mario>
              ^xtile <xt>)
   (<xt> ^centre.isclear <yn>)
   (<mario> ^type <mtype>)
   (<op> ^name action
         ^direction <d>
         ^jump <j>
         ^speed <sp>)
   (<m> ^type <monst>
        ^distx <dx>
        ^disty <sy>
        ^direction <dir>)
--> 
   (<s> ^operator <op> = 0)
}
