### object rl-rule

sp {rl-rule*monster
   :template
   (state <s> ^name mario-soar	
   	      ^operator <op> +
	      ^object <obj>
	      ^reward-link <rl>
	      ^mario <mario>)
   (<rl> ^<obj> <obj-reward>)
   (<op> ^name action
   	 ^direction <d>
	 ^jump <j>
	 ^speed <sp>)
   (<obj> ^type <type>
	  ^rel-dist-x <dx>
	  ^rel-dist-y <dy>
	  ^group monster
	  ^winged <haswing>
	  ^visible true
	  ^horizontal-speed <hs>
	  ^vertical-speed <vs>)
   (<mario> ^type <mtype>)
-->
   (<s> ^operator <op> = 0)
}

sp {rl-rule*block
   :template
   (state <s> ^name mario-soar	
   	      ^operator <op> +
	      ^object <obj>
	      ^reward-link <rl>
	      ^mario <mario>)
   (<rl> ^<obj> <obj-reward>)
   (<op> ^name action
   	 ^direction <d>
	 ^jump <j>
	 ^speed <sp>)
   (<obj> ^type <type>
	  ^rel-dist-x <dx>
	  ^rel-dist-y <dy>
	  -^group monster
	  ^visible true)
   (<mario> ^type <mtype>)
-->
   (<s> ^operator <op> = 0)
}



#### elaborate object reward
sp {elaborate*monster*reward
   (state <s> ^name mario-soar
	      ^reward-link <rl>
	      ^object <obj>)
   (<obj> ^reward <reward>
   	  ^visible true)
-->
   (<rl> ^<obj> <obj-reward>)
   (<obj-reward> ^reward <rr>)
   (<rr> ^value <reward>)
}


### move-right to finish line rl-rules
sp {rl-rule*finish-game
   :template
   (state <s> ^name mario-soar
   	      ^operator <op> +
	      ^tile-type <tt>
	      ^reward-link <rl>)
   (<tt> ^right <tile-right>)
   (<op> ^name action
   	 ^direction <d>
	 ^jump <j>
	 ^speed <sp>)
   (<rl> ^move-right <reward>)
-->
   (<s> ^operator <op> = 0)
}

### elaborate move-right reward
sp {elaborate*finish-game
   (state <s> ^name mario-soar
	      ^reward-link <rl>
	      ^io.input-link.right-reward <reward>)
-->
   (<rl> ^move-right <r-reward>)
   (<r-reward> ^reward.value <reward>)
}  



   	      