#-------------------mario specific elaborations------
#--psuedo elaboration to determine the current id of the episode

sp {mario-soar*query*emem*present-id
   (state <s> ^name mario-soar
              ^epmem.command <ecom>)
-->
   (<ecom> ^query <q>)
   (<q> ^top-state.superstate nil)
}

sp {mario-soar*elaborate*state*present-id
   (state <s> ^name mario-soar              
   	      ^epmem.result <eres>)
   (<eres> ^present-id <pid>)
-->
   (<s> ^present-id <pid>)
}

sp {all*elaborate*state*present-id
   (state <s> ^superstate.present-id <pid>)
-->
   (<s> ^present-id <pid>)
}

#----------------elaborate mario--------------------
sp {mario-soar*elaborate-mario
   (state <s> ^io.input-link.mario <mario>
              ^name mario-soar)
-->
   (<s> ^mario <mario>)
}

#----------------elaborate monsters-----------------

sp {mario-soar*elaborate-monster
   (state <s> ^io.input-link.monsters.monster <m>
              ^name mario-soar)
   (<m> -^type |Mario|)
-->
   (<s> ^object <m>)
}

sp {mario-soar*elaborate-object-relative-distance*float
   (state <s> ^mario <mario>
              ^object <ob>)	
   (<mario> ^xd <mariox>
            ^yd <marioy>)
   (<ob> ^xd <obx>
         ^yd <oby>)
-->
   (<ob> ^distx (- <obx> <mariox>)
         ^disty (- <oby> <marioy>))
}

#------------------elaborate tiles--------------------------
sp {mario-soar*elaborate-xright
   (state <s> ^mario <mario>
   	      ^name mario-soar)
   (<mario> ^xi <x>
        ^yi <y>)
-->
   (<s> ^xtile <xt>
        ^ytile <yt>)
   (<xt> ^right (+ <x> 1)
         ^left (- <x> 1)
         ^centre <x>)
}

sp {mario-soar*elaborate-tile-on-right
   (state <s> ^io.input-link <il>
              ^xtile.right <xloc>
              ^mario.yi <y>
	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <xloc>
        ^type <ty>)
-->
   (<s> ^tile-type.right <ty>)
}
sp {mario-soar*elaborate-coin
   (state <s> ^io.input-link <il>
   	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <x>
        ^type |$|)
-->
   (<s> ^object <c>)
   (<c> ^x <x>
        ^y <y>
	^type coin)
}
sp {mario-soar*elaborate-coin-reachable
   (state <s> ^object <c>
   	      ^name mario-soar)
   (<c> ^distx < 2
        ^disty < 4
	^type coin)
  -(<c> ^distx < -1)
  -(<c> ^disty < 0nn)
-->
   (<c> ^isreachable yes)
}
sp {mario-soar*elaborate-question
   (state <s> ^io.input-link <il>
   	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <x>
        ^type |?|)
-->
   (<s> ^object <q>)
   (<q> ^x <x>
        ^y <y>
	^type question)
}
sp {mario-soar*elaborate-question-reachable
   (state <s> ^question <q>
   	      ^name mario-soar)
   (<q> ^distx < 3
        ^disty < 4)
  -(<q> ^distx < -2)
  -(<q> ^disty < 0)
-->
   (<q> ^isreachable yes)
}