#----------------elaborate mario--------------------
sp {mario-soar*elaborate-mario
   (state <s> ^io.input-link.mario <mario>
              ^name mario-soar)
-->
   (<s> ^mario <mario>)
}

#----------------elaborate monsters-----------------

sp {mario-soar*elaborate-monster
   (state <s> ^io.input-link.monsters.monster <m>
              ^name mario-soar)
   (<m> -^type |Mario|
   	^type <mtype>)
-->
   (<s> ^object <m>)
   (<m>	^group monster)
}


#--------------elaborate blocks-----------------------
sp {mario-soar*elaborate-coin
   (state <s> ^io.input-link <il>
   	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <x>
        ^type |$|
	^reward <r>)
-->
   (<s> ^object <c>)
   (<c> ^x <x>
        ^y <y>
	^type coin
	^reward <r>)
}

sp {mario-soar*elaborate-question
   (state <s> ^io.input-link <il>
   	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <x>
        ^type |?|
	^reward <r>)
-->
   (<s> ^object <q>)
   (<q> ^x <x>
        ^y <y>
	^type question
	^reward <r>)
}

#------elaborate relative distance from objects-----------------
#using integral distances only for now
sp {mario-soar*elaborate-object-relative-distance*float
   (state <s> ^mario <mario>
              ^object <ob>)	
   (<mario> ^x <mariox>
            ^y <marioy>)
   (<ob> ^x <obx>
         ^y <oby>)
-->
   (<ob> ^rel-dist-x (- <obx> <mariox>) 
         ^rel-dist-y (- <oby> <marioy>))
}

#------- elaborate visibility area------------------------------

sp {mario-soar*elaborate*object-visibility*coin
   (state <s> ^mario <mario>
   	      ^object <obj>)
   (<obj> ^rel-dist-x < 2
   	  ^rel-dist-y < 4
	  ^type coin)
   -(<obj> ^rel-dist-x < -1)
   -(<obj> ^rel-dist-y < 0)
-->
   (<obj> ^visible true)
}


sp {mario-soar*elaborate*object-visibility*question
   (state <s> ^mario <mario>
   	      ^object <obj>)
   (<obj> ^rel-dist-x < 3
   	  ^rel-dist-y < 4
	  ^type question)
  -(<obj> ^rel-dist-x < -2)
  -(<obj> ^rel-dist-y < 0)
-->
   (<obj> ^visible true)
}

sp {mario-soar*elaborate*object-visibility*monster
   (state <s> ^mario <mario>
   	      ^object <obj>)
   (<obj> ^rel-dist-x < 6
   	  ^rel-dist-y < 5
	  ^group monster)
  -(<obj> ^rel-dist-x < -5)
  -(<obj> ^rel-dist-y < -5)
-->
   (<obj> ^visible true)
}

#------------------elaborate tiles--------------------------
sp {mario-soar*elaborate-xright
   (state <s> ^mario <mario>
   	      ^name mario-soar)
   (<mario> ^xi <x>
        ^yi <y>)
-->
   (<s> ^xtile <xt>
        ^ytile <yt>)
   (<xt> ^right (+ <x> 1)
         ^left (- <x> 1)
         ^centre <x>)
}

sp {mario-soar*elaborate-tile-on-right
   (state <s> ^io.input-link <il>
              ^xtile.right <xloc>
              ^mario.yi <y>
	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <xloc>
        ^type <ty>)
-->
   (<s> ^tile-type.right <ty>)
}
