sp {propose*tackle-monster1
   (state <s> ^name mario-soar-hrl3
              ^monster <m>
            - ^coin.isreachable yes
            - ^question.isreachable yes)
   (<m> ^isthreat yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name tackle-monster
        ^monster <m>)
}
sp {propose*tackle-monster2
   (state <s> ^name mario-soar-hrl3
              ^monster <m>
              ^coin.isreachable yes
            - ^question.isreachable yes)
   (<m> ^isthreat yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name tackle-monster
        ^monster <m>)
}
sp {propose*tackle-monster3
   (state <s> ^name mario-soar-hrl3
              ^monster <m>
            - ^coin.isreachable yes
              ^question.isreachable yes)
   (<m> ^isthreat yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name tackle-monster
        ^monster <m>)
}
gp {tackle*monster*general
   (state <s> ^name mario-soar-hrl3
              ^operator <op> +)
   (<op> ^name tackle-monster
         ^monster <m>)
   (<m> ^distx [-3 -2 -1 0 1 2 3 4]
        ^disty [-5 -4 -3 -2 -1 0 1 2 3 4]
        ^direction [vertical stationary towards-mario awayfrom-mario]
        ^type  [|Red Koopa| |Green Koopa| |Goomba| |Spikey| |Piranha Plant| |Mushroom| |Fire Flower| |Shell|] 
        ^winged [yes no])
-->
   (<s> ^operator <op> = 0)
}
sp {monster*random*action
   (state <s> ^name tackle-monster
              ^rand-move <mv>
              ^rand-jump <jp>
              ^rand-speed <sp>
              ^monster <m>
              ^mario <mario>)
   (<m> ^distx <dx>
        ^disty <dy>
        ^direction <dir>)
   (<jp> ^up <j>) 
   (<mario> ^yi <my>)
   (<mv> ^direction <d>)
   (<sp> ^degree <sd>)
-->
   (<s> ^operator <o> +)
   (<o> ^name action
        ^direction <d>
        ^jump <j>
        ^speed <sd>)
}
gp {tackle*monster
   (state <s> ^name tackle-monster
              ^operator <op> +
              ^monster <m>
              ^mario <mario>)
   (<mario> ^type [|Small| |Big| |Fiery|]
        ^tile-above [| | |7| b |?| |$| |1| |\|| |!| |0|])
   (<op> ^name action
         ^direction [left nil right]
         ^jump [yes no]
         ^speed [high low])
   (<m> ^type [|Red Koopa| |Green Koopa| |Goomba| |Spikey| |Piranha Plant| |Mushroom| |Fire Flower| |Shell|] 
        ^distx [-3 -2 -1 0 1 2 3 4]
        ^disty [-5 -4 -3 -2 -1 0 1 2 3 4]
        ^direction [vertical stationary towards-mario awayfrom-mario]
        ^winged [yes no])
--> 
   (<s> ^operator <op> = 0)
}
