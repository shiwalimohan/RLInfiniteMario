#----------------elaborate mario--------------------
sp {mario-soar*elaborate-mario
   (state <s> ^io.input-link.mario <mario>
              ^name mario-soar)
-->
   (<s> ^mario <mario>)
}

#----------------elaborate monsters-----------------

sp {mario-soar*elaborate-monster
   (state <s> ^io.input-link.monsters.monster <m>
              ^name mario-soar)
   (<m> -^type |Mario|
   	^type <mtype>)
-->
   (<s> ^object <m>)
}

#### elaborate blocks

sp {mario-soar*elaborate-blocks
   (state <s> ^io.input-link.block-objects.block <b>
   	      ^name mario-soar)
 -->
   (<s> ^object <b>)
}

### elaborate monsters speed, symbolic

sp {mario-soar*elaborate-speed*px
   (state <s> ^name mario-soar
   	      ^object <obj>)
   (<obj> ^group monster
   	  ^sx > 0)
-->
   (<obj> ^horizontal-speed positive)
}

sp {mario-soar*elaborate-speed*nx
   (state <s> ^name mario-soar
   	      ^object <obj>)
   (<obj> ^group monster
   	  ^sx < 0)
-->
   (<obj> ^horizontal-speed negative)
}

sp {mario-soar*elaborate-speed*zx
   (state <s> ^name mario-soar
   	      ^object <obj>)
   (<obj> ^group monster
   	  ^sx 0.0)
-->
   (<obj> ^horizontal-speed stat)
}

sp {mario-soar*elaborate-speed*py
   (state <s> ^name mario-soar
   	      ^object <obj>)
   (<obj> ^group monster
   	  ^sy > 0)
-->
   (<obj> ^vertical-speed positive)
}

sp {mario-soar*elaborate-speed*ny
   (state <s> ^name mario-soar
   	      ^object <obj>)
   (<obj> ^group monster
   	  ^sy < 0)
-->
   (<obj> ^vertical-speed negative)
}

sp {mario-soar*elaborate-speed*zy
   (state <s> ^name mario-soar
   	      ^object <obj>)
   (<obj> ^group monster
   	  ^sy 0.0)
-->
   (<obj> ^vertical-speed stat)
}


#------elaborate relative distance from objects-----------------
#using integral distances only for now
sp {mario-soar*elaborate-object-relative-distance*float
   (state <s> ^mario <mario>
              ^object <ob>)	
   (<mario> ^x <mariox>
            ^y <marioy>)
   (<ob> ^x <obx>
         ^y <oby>)
-->
   (<ob> ^rel-dist-x (- <obx> <mariox>) 
         ^rel-dist-y (- <oby> <marioy>))
}

#------- elaborate visibility area------------------------------

sp {mario-soar*elaborate*object-visibility*coin
   (state <s> ^mario <mario>
   	      ^object <obj>)
   (<obj> ^rel-dist-x < 5
   	  ^rel-dist-y < 5
	  ^type |$|)
   -(<obj> ^rel-dist-x < -2)
   -(<obj> ^rel-dist-y < -2)
-->
   (<obj> ^visible true)
}


sp {mario-soar*elaborate*object-visibility*question
   (state <s> ^mario <mario>
   	      ^object <obj>)
   (<obj> ^rel-dist-x < 5
   	  ^rel-dist-y < 5
	  ^type |?|)
  -(<obj> ^rel-dist-x < -2)
  -(<obj> ^rel-dist-y < -2)
-->
   (<obj> ^visible true)
}

sp {mario-soar*elaborate*object-visibility*monster
   (state <s> ^mario <mario>
   	      ^object <obj>)
   (<obj> ^rel-dist-x < 4
   	  ^rel-dist-y < 3
	  ^group monster)
  -(<obj> ^rel-dist-x < -3)
  -(<obj> ^rel-dist-y < -3)
-->
   (<obj> ^visible true)
}

#------------------elaborate tiles--------------------------
sp {mario-soar*elaborate-xright
   (state <s> ^mario <mario>
   	      ^name mario-soar)
   (<mario> ^xi <x>
        ^yi <y>)
-->
   (<s> ^xtile <xt>
        ^ytile <yt>)
   (<xt> ^right (+ <x> 1)
         ^left (- <x> 1)
         ^centre <x>)
}

sp {mario-soar*elaborate-tile-on-right
   (state <s> ^io.input-link <il>
              ^xtile.right <xloc>
              ^mario.yi <y>
	      ^name mario-soar)
   (<il> ^visual-scene.tile-row <row>)
   (<row> ^y <y>
          ^tile <t>)
   (<t> ^x <xloc>
        ^type <ty>)
-->
   (<s> ^tile-type.right <ty>)
}

